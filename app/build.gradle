apply plugin: 'com.android.application'

android {

    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {

        applicationId "net.vsona.projecta"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "Integer", "SPLASH_TIME", '600'
        buildConfigField "Boolean", "IS_SHOW_LOG", "false"

        jackOptions {
            enabled true
        }
    }

    buildTypes {
        debug {
            buildConfigField "Integer", "SPLASH_TIME", '300'
            buildConfigField "Boolean", "IS_SHOW_LOG", "true"
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            debuggable false
            jniDebuggable false
            multiDexEnabled true
            zipAlignEnabled true
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        releaseLog {
            buildConfigField "Integer", "SPLASH_TIME", '300'
            buildConfigField "Boolean", "IS_SHOW_LOG", "true"
            debuggable false
            jniDebuggable false
            multiDexEnabled true
            zipAlignEnabled true
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        dev {
        }
        pushDev {
        }
        yingyongbao {
        }
        baidu {
        }
        _360 {
        }
        xiaomi {
        }
    }
    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [CHANNEL_VALUE: name]
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (variant.buildType.name.equals('release')) {
                //生成apk名字的格式：projectA-0.0.1_12-14_xiaomi.apk
                //可自定义自己想要生成的格式
                def fileName = "projectA-${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    dexOptions {
        preDexLibraries = false
        jumboMode = false
        javaMaxHeapSize "4096M"
    }

    dataBinding {
//        enabled = true
    }
}

def releaseTime() {
    return new Date().format("MM-dd", TimeZone.getTimeZone("UTC"))
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    compile project(':common')

    testCompile "junit:junit:$rootProject.ext.junitVersion"//
    compile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"//
    compile "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"//
    compile "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    //RxDownload
    compile 'zlc.season:rxdownload2:1.0.4'

    //RxJava
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.0.1'

    //retrofit
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'

    //RxPermission
    compile 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.1@aar'

    //布局比例适配
    compile 'com.zhy:autolayout:1.4.5'
    //
    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'
    //recyclerview
    compile 'com.jude:easyrecyclerview:4.3.6'
}
